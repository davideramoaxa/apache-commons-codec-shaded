// This gradle file fetches commons-codec from mavenCentral, applies shadowing, and installs the result to the local Maven repository.

buildscript {

    repositories {

        mavenCentral()

        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://jitpack.io" }

    }

    dependencies {

        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'

    }

}



apply plugin: 'java'

apply plugin: 'maven-publish'

apply plugin: 'com.github.johnrengelman.shadow'



def ccGroupId = 'commons-codec'

def ccArtifactId = ccGroupId

def ccsArtifactId = ccArtifactId + '-shaded'

def ccVersion = '1.11'



allprojects {

    repositories {

        jcenter()
        maven { url 'https://jitpack.io' }
        mavenCentral()

    }

}


dependencies {

    implementation "${ccGroupId}:${ccArtifactId}:${ccVersion}"

}



shadowJar {

    relocate 'org.apache.commons.codec', 'shaded.org.apache.commons.codec'

    archiveBaseName = ccsArtifactId

    archiveVersion = ccVersion

    archiveClassifier = null

}



publishing {

    publications {

        shadow(MavenPublication) {

            groupId = ccGroupId

            artifactId = ccsArtifactId

            version = ccVersion

            artifact shadowJar

        }

    }

    repositories {

        mavenLocal()

    }

}



defaultTasks 'publishToMavenLocal'
